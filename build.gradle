import org.apache.tools.ant.taskdefs.condition.Os

group 'ru.kos.test-build'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'test-build'
    version '1.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task npm(type: ExecAdvanced) {
    workingDir 'src/client'
    execObject 'npm'
    args "install"
}

task bower(type: ExecAdvanced, dependsOn: npm) {
    workingDir 'src/client'
    execObject 'bower'
    args "install"
}

task gulp(type: ExecAdvanced, dependsOn: bower) {
    workingDir 'src/client'
    execObject 'gulp'
    args "copyFiles"
}

processResources.dependsOn gulp


class ExecAdvanced extends Exec {

    void setExecObject(String execObject) {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            setExecutable(execObject + ".cmd")
        } else {
            setExecutable(execObject);
        }
    }
}